//==============================================================
//        _____ _           _ _
//       / ____| |         | (_)
//      | |  __| | __ _  __| |_ _   _ ___
//      | | |_ | |/ _` |/ _` | | | | / __|
//      | |__| | | (_| | (_| | | |_| \__ \
//       \_____|_|\__,_|\__,_|_|\__,_|___/
//
//===== rAthena Script =========================================
//= Arena PvP Normal, Extreme (Sem Ygg) e GVG
//= Ranking Top 10 PvP e GvG
//= Reset Automático todo dia 1 ou Manualmente
//= Entrega premiação por RoDEX
//= Os jogadores ganham 1 ponto por kill
//= Possui loja para trocar pontos por itens
//= Narrador PVP (Sons ao matar)
//= Tabelas SQL com registro total
//= Mostra a quantidade de jogadores nas arenas
//= Mapa customizado EXCLUSIVO
//= Remove Buffs selecionados pelo administrador
//= Adicionado Delay de pontuação entre kills
//= Adicionado restrição de kill consecutivo do mesmo jogador
//= Adicionado horário para contabilizar os pontos de kill
//===== Importante: ============================================
//= Observação: NÃO ALTERE O NOME NO NPC
//= Observação: Altere manualmente o mapa do Arena_Porteiro.txt
//===== Créditos: ==============================================
//= Gladius e FelipeFePa
//===== Versao do NPC: =========================================
//= 2.0
//===== Loja PVP: ==============================================
-	pointshop	pontospvp	-1,#PONTOS_PVP,31803:200;31803:200;31885:200;
//==============================================================

prontera,158,193,5	script	Arena de Batalha	417,{	goto NPC_PVP;

//==============================================================
// Configurações
OnInit:

//==============================================================
// Nome do NPC
	set .np$,"^00B2EE["+strnpcinfo(1)+"]^000000";
	set .np2$,"[ "+strnpcinfo(1)+" ]";

//==============================================================
// Configuração de Level Minimo
	set .lvlenter, 254;

//==============================================================
// Configuração de Horário de Kill (Formato 24h)
// Obs: Só vai contar kill nessa faixa de horário
	setarray .horarioStart, 07;
	setarray .horarioEnd,   03;

//==============================================================
// Configuração de Delay para Contabilizar Kill (Em Segundos)
// Obs: O jogador só ganha pontos após este delay. 0 = desabilita
	set .delaykill, 25;

//==============================================================
// Configurações de MAPA
	set .servername$, "Odin";	// Nome do Servidor
	set .pvpnormal$, "guild_vs2";	// PvP Normal
	set .pvpextreme$, "guild_vs4";	// PvP Extreme
	set .gvgnormal$, "guild_vs1";	// GvG Normal

//====================================================================================
// Remoção de Buffs
//===== Instruções: ==================================================================
// Para permitir entrar com buffs nas arenas defina: set .removebuff = 0
// Para adicionar novos efeitos a serem removidos, adicione seu nome.
// Documentação: https://github.com/rathena/rathena/blob/master/doc/status_change.txt
	set .removebuff,1;	// 0 = Desativado | 1 = Ativado
	setarray .endbuff[0],	SC_KYRIE,		// Kyrie Eleison
							SC_ASSUMPTIO,	// Assumptio
							SC_SPIRIT;		// Soul Linker
							//SC_ALL;		// Remove Tudo

//==============================================================
// Remover FP de classes que não Alquimistas e Evoluções?
// 1 = Sim | 0 = Não
	set .removefp, 1;

//==============================================================
// Configurações de Premiação

// Premiação PVP
	setarray .mailitem_pvp[0],  501, 502, 503;	// Item
	setarray .mailamount_pvp[0],  5,   1,   1;	// Quantidade

// Premiação GVG
	setarray .mailitem_gvg[0],  504, 505, 506;	// Item
	setarray .mailamount_gvg[0],  5,   1,   1;	// Quantidade

//==============================================================
//== Comando

	bindatcmd("pvp",strnpcinfo(3)+"::OnPvP",0,99);

//==============================================================
//== Mapflag

		setmapflag(.pvpnormal$,MF_NOBRANCH);
		setmapflag(.pvpnormal$,MF_NOMEMO);
		setmapflag(.pvpnormal$,MF_NORETURN);
		setmapflag(.pvpnormal$,MF_NOTELEPORT);
		setmapflag(.pvpnormal$,MF_NOWARP);
		setmapflag(.pvpnormal$,MF_NOWARPTO);
		setmapflag(.pvpnormal$,MF_PVP_NOGUILD);
		setmapflag(.pvpnormal$,MF_NOSAVE);
		setmapflag(.pvpnormal$,MF_PVP);

		setmapflag(.pvpextreme$,MF_NOBRANCH);
		setmapflag(.pvpextreme$,MF_NOMEMO);
		setmapflag(.pvpextreme$,MF_NORETURN);
		setmapflag(.pvpextreme$,MF_NOTELEPORT);
		setmapflag(.pvpextreme$,MF_NOWARP);
		setmapflag(.pvpextreme$,MF_NOWARPTO);
		setmapflag(.pvpextreme$,MF_PVP_NOGUILD);
		setmapflag(.pvpextreme$,MF_NOSAVE);
		setmapflag(.pvpextreme$,MF_PVP);
		setmapflag(.pvpextreme$,MF_NOITEMCONSUMPTION);

		setmapflag(.gvgnormal$,MF_NOBRANCH);
		setmapflag(.gvgnormal$,MF_NOMEMO);
		setmapflag(.gvgnormal$,MF_NORETURN);
		setmapflag(.gvgnormal$,MF_NOTELEPORT);
		setmapflag(.gvgnormal$,MF_NOWARP);
		setmapflag(.gvgnormal$,MF_NOWARPTO);
		setmapflag(.gvgnormal$,MF_PVP_NOGUILD);
		setmapflag(.gvgnormal$,MF_NOSAVE);
		setmapflag(.gvgnormal$,MF_GVG);

//==============================================================
//== Não Mexer
	set .GMAccess,99;	// Acesso ADM
	set .maxmailitem_pvp, getarraysize(.mailitem_pvp);
	set .maxmailitem_gvg, getarraysize(.mailitem_gvg);
	set .maxhorariostart, getarraysize(.horarioStart);
	set .maxhorarioend, getarraysize(.horarioEnd);

//==============================================================
// Chat PvP

//chat_novamente:
	waitingroom "PvP Normal: "+getmapusers (.pvpnormal$)+"",0;
	sleep 3000;
	delwaitingroom;
	waitingroom "PvP Extreme: "+getmapusers (.pvpextreme$)+"",0;
	sleep 3000;
	delwaitingroom;
	waitingroom "GvG Normal: "+getmapusers (.gvgnormal$)+"",0;
	sleep 3000;
	delwaitingroom;
//goto chat_novamente;

//==============================================================
// Safe Zone

	//setcell .pvpnormal$,4,20,95,4,cell_basilica,1;
	//setcell .pvpnormal$,20,22,4,94,cell_basilica,1;
	//setcell .pvpnormal$,22,95,95,77,cell_basilica,1;
	//setcell .pvpnormal$,95,76,79,22,cell_basilica,1;

	//setcell .pvpextreme$,4,20,95,4,cell_basilica,1;
	//setcell .pvpextreme$,20,22,4,94,cell_basilica,1;
	//setcell .pvpextreme$,22,95,95,77,cell_basilica,1;
	//setcell .pvpextreme$,95,76,79,22,cell_basilica,1;

	//setcell .gvgnormal$,4,20,95,4,cell_basilica,1;
	//setcell .gvgnormal$,20,22,4,94,cell_basilica,1;
	//setcell .gvgnormal$,22,95,95,77,cell_basilica,1;
	//setcell .gvgnormal$,95,76,79,22,cell_basilica,1;

end;

// Fim das Configurações
//==============================================================
// Script

OnPvP:
	getmapxy(.@maps$, .@x, .@y, BL_PC);
	if(getmapflag(.@maps$, MF_PVP) || getmapflag(.@maps$, MF_GVG) || getmapflag(.@maps$, MF_BATTLEGROUND)) {
		dispbottom .np2$+" : O comando @pvp não pode ser usado neste mapa.";
		end;
	}

NPC_PVP:
	mes .np$;
	mes (gettime(3)>= 6&&gettime(3)<= 12?"Bom dia":(gettime(3)>=13&&gettime(3)<=18?"Boa tarde":"Boa noite"))+", ^FFA500"+strcharinfo(0)+"^000000! Você possui: ^FFA500"+#PONTOS_PVP+"^000000 ^00B2EEPontos PvP^000000, o que deseja fazer?";
	query_sql "SELECT `char_name` FROM `ranking_pvp` WHERE '"+getcharid(O)+"' ORDER BY `Total` DESC LIMIT 1",@nome$;
	query_sql "SELECT `guild_name` FROM `ranking_gvg` WHERE '"+getcharid(2)+"' ORDER BY `Total` DESC LIMIT 1",@nomeg$;
	mes " ";
	mes "^00B2EE- Líder PvP^000000: ^FFA500["+@nome$+"]^000000";
	mes "^00B2EE- Líder GvG^000000: ^FFA500["+@nomeg$+"]^000000";
	switch(select("^3CB371[»]^000000 Entrar na Arena:^FFA500[»]^000000 Ver Ranking:^FFA500[»]^000000 Loja PvP:^ff0000[»]^000000 Sair:"+((getgmlevel()<.GMAccess || !getgmlevel())?"":"^ff0000[»]^000000 Resetar Ranking")+"")) {

//==============================================================
//= Arenas

		case 1:
		next;
		mes .np$;
		mes "Qual arena você deseja entrar?";
		mes "^ff0000Ao entrar na Arena, alguns dos seus buffs serão removidos.^000000";
		switch(select("^FFA500^3CB371[»]^000000 Arena PvP^000000 ^FF0000["+getmapusers (.pvpnormal$)+"]^000000:^FFA500^3CB371[»]^000000 Arena Extreme^000000 ^FF0000["+getmapusers (.pvpextreme$)+"]^000000:^FFA500^3CB371[»]^000000 Arena GvG^000000 ^FF0000["+getmapusers (.gvgnormal$)+"]^000000:^ff0000[»]^000000 Sair")){

//==============================================================
//= Arena PvP

			case 1:
			next;
			mes .np$;
			mes "Boa Sorte.";
			announce .np2$+" : "+strcharinfo(0)+" entrou na Arena PvP!",bc_map,0xFF0000;
			if(.removebuff){
				for(.@i=0;.@i<getarraysize(.endbuff);.@i++)
					sc_end .endbuff[.@i];
			}
			if(.removefp) callsub(check_fp);
			switch( rand(4) ) {
				case 0:	warp .pvpnormal$,50,16;	end;
				case 1:	warp .pvpnormal$,50,85;	end;
				case 2:	warp .pvpnormal$,14,50;	end;
				case 3:	warp .pvpnormal$,85,50;	end;
			}
			close;

//==============================================================
//= Arena Extreme

			case 2:
			next;
			mes .np$;
			mes "Boa Sorte.";
			announce .np2$+" : "+strcharinfo(0)+" entrou na Arena Extreme!",bc_map,0xFF0000;
			if(.removebuff){
				for(.@i=0;.@i<getarraysize(.endbuff);.@i++)
					sc_end .endbuff[.@i];
			}
			if(.removefp) callsub(check_fp);
			switch( rand(4) ) {
				case 0:	warp .pvpextreme$,50,16;	end;
				case 1:	warp .pvpextreme$,50,85;	end;
				case 2:	warp .pvpextreme$,14,50;	end;
				case 3:	warp .pvpextreme$,85,50;	end;
			}
			close;

//==============================================================
//= Arena GvG

			case 3:
			next;
			if(!getcharid(2)){
				mes .np$;
				mes "Desculpe, mas você precisar estar em uma guild para entrar na arena GvG.";
				close;
			}
			mes .np$;
			mes "Boa Sorte.";
			announce .np2$+" : "+strcharinfo(0)+" entrou na Arena GVG!",bc_map,0xFF0000;
			if(.removebuff){
				for(.@i=0;.@i<getarraysize(.endbuff);.@i++)
					sc_end .endbuff[.@i];
			}
			if(.removefp) callsub(check_fp);
			switch( rand(4) ) {
				case 0:	warp .gvgnormal$,50,16;	end;
				case 1:	warp .gvgnormal$,50,85;	end;
				case 2:	warp .gvgnormal$,14,50;	end;
				case 3:	warp .gvgnormal$,85,50;	end;
			}
			close;

//==============================================================
//= Sair

			case 4:
			close;
		}

//==============================================================
//= Ranking

		case 2:
		next;
		mes .np$;
		mes "Selecione qual ranking deseja visualizar.";
		switch(select("^3CB371[»]^000000 PvP:^3CB371[»]^000000 GvG:^3CB371[»]^000000 Ranking Pessoal:^FFA500[»]^000000 Informações:^ff0000[»]^000000 Sair")){

			case 1:
			next;
			query_sql "SELECT `char_name`, `Matou`, `Morreu`, `Total`  FROM `ranking_pvp` ORDER BY `Total` DESC LIMIT 10",.@nome$,.@matou,.@morreu,.@pontos;
			mes .np$;
			mes "Posição - ^228B22Matou^000000 - ^EE0000Morreu^000000 - ^00B2EEPontos^000000";
			for (set .@i, 0; .@i < 10; set .@i, .@i +1)
				mes (.@i+1)+"º^000000 - ^FF8C00"+.@nome$[.@i]+"^000000 - ^228B22"+.@matou[.@i]+"^000000 - ^EE0000"+.@morreu[.@i]+"^000000 - ^00B2EE"+.@pontos[.@i]+"^000000";
			close;

			case 2:
			next;
			query_sql "SELECT `guild_name`, `Matou`, `Morreu`, `Total`  FROM `ranking_gvg` ORDER BY `Total` DESC LIMIT 10",.@nomeg$,.@matoug,.@morreug,.@pontosg;
			mes .np$;
			mes "Posição - Matou - Morreu - Pontos";
			for (set .@i, 0; .@i < 10; set .@i, .@i +1)
				mes (.@i+1)+"º^000000 - ^FF8C00"+.@nomeg$[.@i]+"^000000 - ^00B2EE"+.@matoug[.@i]+"^000000 - ^EE0000"+.@morreug[.@i]+"^000000 - ^00B2EE"+.@pontosg[.@i]+"^000000";
			close;

//==============================================================
//= Ranking Pessoal

			case 3:
			next;
			mes .np$;
			query_sql "SELECT `char_id`,`char_name`, `Matou`, `Morreu`, `Total`  FROM `ranking_pvp` ORDER BY `Total` DESC",.@char_id,.@nome$,.@matou,.@morreu,.@pontos;
			.@num = inarray(.@char_id,getcharid(0));
			if(.@num < 0)
				mes "Você ainda não esta rankeado.";
			else{
				mes "Posição - ^228B22Matou^000000 - ^EE0000Morreu^000000 - ^00B2EEPontos^000000";
				mes (.@num+1)+"º^000000 - ^FF8C00"+.@nome$[.@num]+"^000000 - ^228B22"+.@matou[.@num]+"^000000 - ^EE0000"+.@morreu[.@num]+"^000000 - ^00B2EE"+.@pontos[.@num]+"^000000";
			}
			end;

//==============================================================
//= Informações

			case 4:
			next;
			mes .np$;
			mes "Todos os Rankings fazem a contagem de ^228B22Matou^000000 (Quantidade de player que você matou), ^EE0000Morreu^000000 (Quantidade de vezes que você morreu) e soma os seus ^00B2EEPontos^000000.";
			next;
			mes .np$;
			mes "Cada vez que você derrotar algum jogador no PvP (apenas no PvP) você recebe 1 Ponto PvP, esses pontos podem ser trocados por itens.";
			next;
			mes .np$;
			mes "Os 3 primeiros personagens do Ranking PvP recebem uma premiação no ultimo dia de cada mês, em seguida o ranking é resetado e recomeça a contagem.";
			next;
			mes .np$;
			mes "Apenas o primeiro lugar do Ranking GVG é premiado. A premiação vai para o líder da guilda e fica a critério dele a distribuição entre os membros do seu clã.";
			next;
			mes .np$;
			mes "Selecione de qual Ranking você deseja se informar sobre as premiações:";
			switch(select("^FFA500[»]^000000 Premiação PvP:^FFA500[»]^000000 Premiação GvG:^ff0000[»]^000000 Sair")){

				case 1:
				next;
				mes .np$;
				for(.@i = 0; .@i < getarraysize(.mailitem_pvp); .@i++)
					mes .mailamount_pvp[.@i]+" <ITEM>"+getitemname(.mailitem_pvp[.@i])+"<INFO>"+.mailitem_pvp[.@i]+"</INFO></ITEM>";
				close;

				case 2:
				next;
				mes .np$;
				for(.@i = 0; .@i < getarraysize(.mailitem_gvg); .@i++)
					mes .mailamount_gvg[.@i]+" <ITEM>"+getitemname(.mailitem_gvg[.@i])+"<INFO>"+.mailitem_gvg[.@i]+"</INFO></ITEM>";
				close;

				case 3:
				next;
				mes .np$;
				mes "Ok, volte quando precisar dos meus serviços.";
				close;
			}

			case 5:
			next;
			mes .np$;
			mes "Ok, volte quando precisar dos meus serviços.";
			close;

		}

//==============================================================
// Loja PvP

		case 3:
		close2;
		callfunc "lojapvp";
		end;

//=================================================================
//== Sair

		case 4:
		next;
		mes .np$;
		mes "Ok, volte quando precisar dos meus serviços.";
		close;

//=================================================================
//== Resetar Ranking

		case 5:
		next;
		mes .np$;
		mes "Olá ^FFA500"+strcharinfo(0)+"^000000. Qual ranking você deseja resetar?";
		switch(select("^3CB371[»]^000000 Ranking PvP:^3CB371[»]^000000 Ranking GvG:^ff0000[»]^000000 Sair")) {

//=================================================================
//== Resetar Ranking PvP

			case 1:
			next;
			mes .np$;
			mes "Deseja entregar a premiação dos 3 primeiros colocados do ranking antes de resetar?";
			if (select("^3CB371[»]^000000 Sim:^ff0000[»]^000000 Não") == 2) {
				next;
				mes .np$;
				mes "Ranking PvP resetado com sucesso! Não houve entrega de premiação para nenhum jogador.";
				close2;
				announce .np2$+" : Ranking PvP resetado por: "+strcharinfo(0)+". Não houve premiação para nenhum jogador.",bc_map,0xFF10FF;
				query_sql "INSERT INTO `ranking_pvp_reset` (`account_id`, `char_id`, `Data`, `Premio?`) VALUES ('"+ getcharid(3) +"', '"+ getcharid(0) +"', '"+gettimestr("%d/%m/%Y - %H:%M:%S", 25)+"', 'Não')";
				query_sql "truncate table ranking_pvp";
				end;
			}

			next;
			mes .np$;
			mes "Ranking PvP resetado com sucesso! Premiação entregue para os jogadores:";
			query_sql "SELECT `char_id`, `char_name` FROM `ranking_pvp` ORDER BY `Total` DESC LIMIT 3",.@char_id, .@nome$;
			.@sender$ = .servername$;
			.@title$ = "Prêmio Ranking PvP";
			.@body$ = "Você está recebendo sua premiação por estar entre os três melhores jogadores do Ranking PvP deste mês!";
			.@zeny = 0;
			.@maxmailitem_pvp = getvariableofnpc(.maxmailitem_pvp,"Arena de Batalha");

			for(.@e = 0; .@e < .@maxmailitem_pvp; .@e++){
				.@mailitem_pvp[.@e] = getvariableofnpc(.mailitem_pvp[.@e],"Arena de Batalha");
				.@mailamount_pvp[.@e] = getvariableofnpc(.mailamount_pvp[.@e],"Arena de Batalha");
			}

			for (.@i = 0; .@i < 3; .@i++) {
				mes (.@i+1)+"º^000000 - ^FF8C00"+.@nome$[.@i]+"^000000";
				mail .@char_id[.@i], .@sender$, .@title$, .@body$, .@zeny, .@mailitem_pvp, .@mailamount_pvp;
			}

			close2;
			announce .np2$+" : Ranking PvP resetado por: "+strcharinfo(0)+". Premiação entregue automaticamente para os jogadores: "+.@nome$[0]+", "+.@nome$[1]+" e "+.@nome$[2]+".",bc_map,0xFF10FF;
			query_sql "INSERT INTO `ranking_pvp_reset` (`account_id`, `char_id`, `Data`, `Premio?`) VALUES ('"+ getcharid(3) +"', '"+ getcharid(0) +"', '"+gettimestr("%d/%m/%Y - %H:%M:%S", 25)+"', 'Sim')";
			query_sql "truncate table ranking_pvp";
			end;

//=================================================================
//== Resetar Ranking GvG

			case 2:
			next;
			mes .np$;
			mes "Deseja entregar a premiação da Guilda vencedora do ranking GvG antes de resetar?";
			if (select("^3CB371[»]^000000 Sim:^ff0000[»]^000000 Não") == 2) {
				next;
				mes .np$;
				mes "Ranking GvG resetado com sucesso! Não houve entrega de premiação para nenhuma guilda.";
				close2;
				announce .np2$+" : Ranking GvG resetado por: "+strcharinfo(0)+". Não houve premiação para nenhuma guilda.",bc_map,0xFF10FF;
				query_sql "INSERT INTO `ranking_gvg_reset`  (`account_id`, `char_id`, `Data`, `Premio?`) VALUES ('"+ getcharid(3) +"', '"+ getcharid(0) +"', '"+gettimestr("%d/%m/%Y - %H:%M:%S", 25)+"', 'Não')";
				query_sql "truncate table ranking_gvg";
				end;
			}

			next;
			mes .np$;
			mes "Ranking GvG resetado com sucesso! Premiação entregue para a guilda:";
			query_sql "SELECT `guild_id`, `guild_name` FROM `ranking_gvg` ORDER BY `Total` DESC LIMIT 1",.@guild_id, .@nome$;
			.@char_id = getguildmasterid(.@guild_id[.@i]);
			.@name_guildm$ = getguildmaster(.@guild_id[.@i]);
			.@sender$ = .servername$;
			.@title$ = "Prêmio Ranking GvG";
			.@body$ = "Você está recebendo essa premiação por sua Guilda ser a melhor posicionada no Ranking GvG deste mês!";
			.@zeny = 0;
			.@maxmailitem_gvg = getvariableofnpc(.maxmailitem_gvg,"Arena de Batalha");

			for(.@e = 0; .@e < .@maxmailitem_gvg; .@e++){
				.@mailitem_gvg[.@e] = getvariableofnpc(.mailitem_pvp[.@e],"Arena de Batalha");
				.@mailamount_gvg[.@e] = getvariableofnpc(.mailamount_gvg[.@e],"Arena de Batalha");
			}

			for (.@i = 0; .@i < 3; .@i++) {
				mes (.@i+1)+"º^000000 - ^FF8C00"+.@nome$[.@i]+"^000000";
				mail .@char_id[.@i], .@sender$, .@title$, .@body$, .@zeny, .@mailitem_gvg, .@mailamount_gvg;
			}

			close2;
			announce .np2$+" : Ranking GvG resetado por: "+strcharinfo(0)+". Premiação entregue automaticamente para o Guild Líder do clã: "+.@name_guildm$+".",bc_map,0xFF10FF;
			query_sql "INSERT INTO `ranking_gvg_reset`  (`account_id`, `char_id`, `Data`, `Premio?`) VALUES ('"+ getcharid(3) +"', '"+ getcharid(0) +"', '"+gettimestr("%d/%m/%Y - %H:%M:%S", 25)+"', 'Sim')";
			query_sql "truncate table ranking_gvg";
			end;

//=================================================================
//== Sair

			case 3:
			close;

		}
	}

//=================================================================
//== Verificação de Nível

check_lvl:
	if (BaseLevel < .lvlenter) {
		mes "Me desculpe, o nível necessário para entrar na arena é ^FFA500"+.lvlenter+"^000000.";
		close;
	}
	return;

//=================================================================
//== Verificação de FP

check_fp:
    if (Class != 18 && Class != 4019 && Class != 4041 && Class != 4071 && Class != 4078 && Class != 4107){
        sc_end SC_CP_WEAPON;
        sc_end SC_CP_SHIELD;
        sc_end SC_CP_ARMOR;
        sc_end SC_CP_HELM;
    }
    return;

}

//============================================================
// Duplicates (Cidades)

morocc,166,93,7	duplicate(Arena de Batalha)	Arena de Batalha#2	801
geffen,124,73,7	duplicate(Arena de Batalha)	Arena de Batalha#3	801
payon,170,222,7	duplicate(Arena de Batalha)	Arena de Batalha#4	801
alberta,121,60,7	duplicate(Arena de Batalha)	Arena de Batalha#5	801
izlude,135,123,7	duplicate(Arena de Batalha)	Arena de Batalha#6	801
aldebaran,194,141,7	duplicate(Arena de Batalha)	Arena de Batalha#7	801
xmas,141,132,7	duplicate(Arena de Batalha)	Arena de Batalha#8	801
comodo,202,142,7	duplicate(Arena de Batalha)	Arena de Batalha#9	801
yuno,153,53,1	duplicate(Arena de Batalha)	Arena de Batalha#10	801
amatsu,203,93,7	duplicate(Arena de Batalha)	Arena de Batalha#11	801
umbala,88,155,7	duplicate(Arena de Batalha)	Arena de Batalha#12	801
niflheim,200,185,7	duplicate(Arena de Batalha)	Arena de Batalha#13	801
louyang,212,52,1	duplicate(Arena de Batalha)	Arena de Batalha#14	801
jawaii,246,142,7	duplicate(Arena de Batalha)	Arena de Batalha#15	801
ayothaya,154,117,7	duplicate(Arena de Batalha)	Arena de Batalha#16	801
einbroch,55,200,7	duplicate(Arena de Batalha)	Arena de Batalha#17	801
lighthalzen,164,85,7	duplicate(Arena de Batalha)	Arena de Batalha#18	801
einbech,77,108,7	duplicate(Arena de Batalha)	Arena de Batalha#19	801
hugel,102,151,7	duplicate(Arena de Batalha)	Arena de Batalha#20	801
rachel,137,110,7	duplicate(Arena de Batalha)	Arena de Batalha#21	801
veins,209,130,7	duplicate(Arena de Batalha)	Arena de Batalha#22	801
moscovia,229,198,7	duplicate(Arena de Batalha)	Arena de Batalha#23	801
mid_camp,220,286,7	duplicate(Arena de Batalha)	Arena de Batalha#24	801
brasilis,201,224,7	duplicate(Arena de Batalha)	Arena de Batalha#25	801
gonryun,155,121,7	duplicate(Arena de Batalha)	Arena de Batalha#26	801
splendide,209,153,7	duplicate(Arena de Batalha)	Arena de Batalha#27	801
manuk,297,241,7	duplicate(Arena de Batalha)	Arena de Batalha#28	801
dicastes01,205,203,7	duplicate(Arena de Batalha)	Arena de Batalha#29	801
