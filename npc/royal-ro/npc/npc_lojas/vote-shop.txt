-	shop	VOTEPOINTS_shop	-1,512:15
prontera.gat,162,180,5	script	Loja de Votos	402,{

	//delwaitingroom;
	//waitingroom "Loja de Votos",0;

	set .name$,"[^FF0000  Loja Vote  ^000000]";
	set @Currency, -1;  // -1 = Vote Points, qualquer outro valor = ID do item

	mes .name$;
	if (@Currency == -1) {
		mes "Esta loja usa ^FF0000Pontos de Vote^000000 para comprar os itens.";
		mes "Você possui atualmente "+#VOTEPOINTS+" Pontos de Vote.";
	} else {
		mes "Esta loja usa ^FF0000"+getitemname(@Currency)+"^000000 para comprar os itens.";
		mes "Você possui atualmente "+countitem(@Currency)+" "+getitemname(@Currency)+".";
	}

	deletearray .ItemLists[0],getarraysize(.ItemLists);
	deletearray .ItemPrice[0],getarraysize(.ItemPrice);
	npcshopitem "VOTEPOINTS_shop",512,1;

	select(
		"^FF4500[ ]^000000 Loja",
		"^1E90FF[ ]^000000 Sair"
	);
	callsub CaseCH,@menu;

	npcshopdelitem "VOTEPOINTS_shop",512;
	for(set .@i,0; .@i < getarraysize(.ItemLists); set .@i,.@i+1)
		npcshopadditem "VOTEPOINTS_shop",.ItemLists[.@i],.ItemPrice[.@i];

	close2;
	callshop "VOTEPOINTS_shop",1;
	npcshopattach "VOTEPOINTS_shop";
	if (@Currency == -1)
		dispbottom "Você possui atualmente "+#VOTEPOINTS+" Pontos de Vote.";
	else if (@Currency > 0)
		dispbottom "Você possui atualmente "+countitem(@Currency)+" "+getitemname(@Currency)+".";

	end;

OnBuyItem:
	// Calcula o custo total
	set @TotalCost, 0;
	for(set @i,0; @i < getarraysize(@bought_nameid); set @i,@i+1) {
		for(set @j,0; @j < getarraysize(.ItemLists); set @j,@j+1) {
			if(.ItemLists[@j] == @bought_nameid[@i]) {
				set @TotalCost, @TotalCost + (.ItemPrice[@j] * @bought_quantity[@i]);
				break;
			}
		}
	}

	// Verifica se tem moeda suficiente
	if (@Currency == -1) {
		if (#VOTEPOINTS < @TotalCost) {
			set @notEnough, 1;
		}
	} else if (@Currency > 0) {
		if (countitem(@Currency) < @TotalCost) {
			set @notEnough, 1;
		}
	}

	if (@notEnough) {
		for(set @i,0; @i < getarraysize(@bought_nameid); set @i,@i+1) {
			mes "Item : ^0000FF"+getitemname(@bought_nameid[@i])+"^000000";
			mes "Quantidade : ^0000FF"+@bought_quantity[@i]+"^000000";
		}
		mes "^00FF00____________________________^000000";
		if (@Currency == -1)
			mes "Custo total : ^0000FF"+@TotalCost+" x Pontos de Vote^000000";
		else
			mes "Custo total : ^0000FF"+@TotalCost+" x "+getitemname(@Currency)+"^000000";
		mes "^00FF00____________________________^000000";
		if (@Currency == -1)
			mes "[ ^FF0000X^000000 ] Você não possui ^0000FFPontos de Vote^000000 suficientes para realizar esta compra.";
		else
			mes "[ ^FF0000X^000000 ] Você não possui ^0000FF"+getitemname(@Currency)+"^000000 suficientes para realizar esta compra.";
	} else {
		// Processa a compra
		for(set @i,0; @i < getarraysize(@bought_nameid); set @i,@i+1) {
			getitem @bought_nameid[@i],@bought_quantity[@i];
			message strcharinfo(0),"Comprou "+getitemname(@bought_nameid[@i]);
		}

		// Deduz a moeda
		if (@Currency == -1) {
			set #VOTEPOINTS, #VOTEPOINTS - @TotalCost;
		} else if (@Currency > 0) {
			delitem @Currency,@TotalCost;
		}

		mes "Obrigado pela compra!";
		if (@Currency == -1)
			mes "Agora você possui "+#VOTEPOINTS+" Pontos de Vote.";
		else
			mes "Agora você possui "+countitem(@Currency)+" "+getitemname(@Currency)+".";
	}

	// Limpa variáveis
	set @TotalCost,0;
	set @notEnough,0;
	deletearray @bought_nameid[0],getarraysize(@bought_nameid);
	deletearray @bought_quantity[0],getarraysize(@bought_quantity);
	close;

CaseCH:
	switch(getarg(0)) {

			Case 1:  // Itens
			set @Currency, -1;
			setarray .ItemLists[0],
				13989,     // Caixa de Bomba Ácida [10]
				12208,     // Manual de Combate
				13995,     // Caixa de Conversores [10]
				12103,     // Galho Sangrento
				12210,     // Goma de Mascar 50%
				14004,     // Caixa de Veneno Mortal [10]
				13718,     // Caixa de Poção Azul Compacta [10]
				13720,     // Caixa de Poção Amarela Compacta [10]
				7621,      // Amuleto de Ziegfried
				13973,     // Caixa de Mega Resistência [5]
				13716,     // Caixa de Poção Média de Vida [50]
				13751,     // Caixa de Poção Branca Média [10]
				13757;     // Caixa de Poção Azul Média [10]

			setarray .ItemPrice[0],
				2,    // Caixa de Bomba Ácida [10]
				4,    // Manual de Combate
				5,    // Caixa de Conversores [10]
				5,    // Galho Sangrento
				8,    // Goma de Mascar 50%
				8,    // Caixa de Veneno Mortal [10]
				10,   // Caixa de Poção Azul Compacta [10]
				10,   // Caixa de Poção Amarela Compacta [10]
				12,   // Amuleto de Ziegfried
				15,   // Caixa de Mega Resistência [5]
				25,   // Caixa de Poção Média de Vida [50]
				30,   // Caixa de Poção Branca Média [10]
				30;   // Caixa de Poção Azul Média [10]
			return;

		Case 2:  // Sair
			close;
			return;

		default:
			mes "Seleção ou configuração inválida.";
			close;
	}

		OnInit:
	.NPCName$ = callfunc("F_NpcInitStatus", "Loja de Votos");
	setunitdata getnpcid(0), UNPC_GROUP_ID, 27;
	end;
}
