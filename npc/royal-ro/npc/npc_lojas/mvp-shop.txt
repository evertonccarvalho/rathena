-	shop	MVPPOINTS_shop	-1,512:15
prontera.gat,158,180,5	script	Loja de MVP	4_EP20_LASGAND,{

	//delwaitingroom;
	//waitingroom "Loja de MVP",0;

	set .name$,"[^FF0000  Loja MVP  ^000000]";
	set @Currency, -1;  // -1 = MVP Points, qualquer outro valor = ID do item

	mes .name$;
	if (@Currency == -1) {
		mes "Esta loja usa ^FF0000Pontos de MVP^000000 para comprar os itens.";
		mes "Você possui atualmente "+#MVPPOINTS+" Pontos de MVP.";
	} else {
		mes "Esta loja usa ^FF0000"+getitemname(@Currency)+"^000000 para comprar os itens.";
		mes "Você possui atualmente "+countitem(@Currency)+" "+getitemname(@Currency)+".";
	}

	deletearray .ItemLists[0],getarraysize(.ItemLists);
	deletearray .ItemPrice[0],getarraysize(.ItemPrice);
	npcshopitem "MVPPOINTS_shop",512,1;

	select(
		"^FF4500[ ]^000000 Loja MVP",
		"^1E90FF[ ]^000000 Sair"
	);

	callsub CaseCH,@menu;

	npcshopdelitem "MVPPOINTS_shop",512;
	for(set .@i,0; .@i < getarraysize(.ItemLists); set .@i,.@i+1)
		npcshopadditem "MVPPOINTS_shop",.ItemLists[.@i],.ItemPrice[.@i];

	close2;
	callshop "MVPPOINTS_shop",1;
	npcshopattach "MVPPOINTS_shop";
	if (@Currency == -1)
		dispbottom "Você possui atualmente "+#MVPPOINTS+" Pontos de MVP.";
	else if (@Currency > 0)
		dispbottom "Você possui atualmente "+countitem(@Currency)+" "+getitemname(@Currency)+".";

	end;

OnBuyItem:
	// Calcula o custo total
	set @TotalCost, 0;
	for(set @i,0; @i < getarraysize(@bought_nameid); set @i,@i+1) {
		for(set @j,0; @j < getarraysize(.ItemLists); set @j,@j+1) {
			if(.ItemLists[@j] == @bought_nameid[@i]) {
				set @TotalCost, @TotalCost + (.ItemPrice[@j] * @bought_quantity[@i]);
				break;
			}
		}
	}

	// Verifica se tem moeda suficiente
	if (@Currency == -1) {
		if (#MVPPOINTS < @TotalCost) {
			set @notEnough, 1;
		}
	} else if (@Currency > 0) {
		if (countitem(@Currency) < @TotalCost) {
			set @notEnough, 1;
		}
	}

	if (@notEnough) {
		for(set @i,0; @i < getarraysize(@bought_nameid); set @i,@i+1) {
			mes "Item : ^0000FF"+getitemname(@bought_nameid[@i])+"^000000";
			mes "Quantidade : ^0000FF"+@bought_quantity[@i]+"^000000";
		}
		mes "^00FF00____________________________^000000";
		if (@Currency == -1)
			mes "Custo total : ^0000FF"+@TotalCost+" x Pontos de MVP^000000";
		else
			mes "Custo total : ^0000FF"+@TotalCost+" x "+getitemname(@Currency)+"^000000";
		mes "^00FF00____________________________^000000";
		if (@Currency == -1)
			mes "[ ^FF0000X^000000 ] Você não possui ^0000FFPontos de MVP^000000 suficientes para realizar esta compra.";
		else
			mes "[ ^FF0000X^000000 ] Você não possui ^0000FF"+getitemname(@Currency)+"^000000 suficientes para realizar esta compra.";
	} else {
		// Processa a compra
		for(set @i,0; @i < getarraysize(@bought_nameid); set @i,@i+1) {
			getitem @bought_nameid[@i],@bought_quantity[@i];
			message strcharinfo(0),"Comprou "+getitemname(@bought_nameid[@i]);
		}

		// Deduz a moeda
		if (@Currency == -1) {
			set #MVPPOINTS, #MVPPOINTS - @TotalCost;
		} else if (@Currency > 0) {
			delitem @Currency,@TotalCost;
		}

		mes "Obrigado pela compra!";
		if (@Currency == -1)
			mes "Agora você possui "+#MVPPOINTS+" Pontos de MVP.";
		else
			mes "Agora você possui "+countitem(@Currency)+" "+getitemname(@Currency)+".";
	}

	// Limpa variáveis
	set @TotalCost,0;
	set @notEnough,0;
	deletearray @bought_nameid[0],getarraysize(@bought_nameid);
	deletearray @bought_quantity[0],getarraysize(@bought_quantity);
	close;

CaseCH:
	switch(getarg(0)) {
			Case 1:  // Itens
			set @Currency, -1;
			setarray .ItemLists[0],
				12914,     // Caixa Ágil [10]
				12913,     // Caixa Abençoada [10]
				24021,     // Caixa de Mastela [100]
				24017,     // Caixa de Frascoi de Revestimento [5]
				13989,     // Caixa de Bomba Ácida [10]
				14004,     // Caixa de Veneno Mortal [10]
				7621,      // Amuleto de Ziegfried
				5363,      // Helmo do Abismo
				2546,      // Capuz de Praia
				2441,      // Sandália de Praia
				5314,      // Máscara do Mason
				5800;      // Rubor de Batalha

		setarray .ItemPrice[0],
				5,    // Caixa Ágil [10]
				5,    // Caixa Abençoada [10]
				5,    // Caixa de Mastela [100]
				10,   // Caixa de Frascoi de Revestimento [5]
				10,   // Caixa de Bomba Ácida [10]
				10,   // Caixa de Veneno Mortal [10]
				10,   // Amuleto de Ziegfried
				300,  // Helmo do Abismo
				500,  // Capuz de Praia
				500,  // Sandália de Praia
				750,  // Máscara do Mason
				1000; // Rubor de Batalha

		return;


		Case 2:  // Sair
			close;
			return;

		default:
			mes "Seleção ou configuração inválida.";
			close;
	}

	OnInit:
	.NPCName$ = callfunc("F_NpcInitStatus", "Loja de MVP");
	setunitdata getnpcid(0), UNPC_GROUP_ID, 37;
	end;

}
