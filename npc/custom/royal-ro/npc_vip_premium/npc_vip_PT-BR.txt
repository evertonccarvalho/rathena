prontera,161,195,3	script	Central VIP	108,{
	OnTalk:
		callsub ShowMainMenu;
		end;

	OnInit:
		.npc_name$ = "^FF7F00[ Central VIP ]^000000";
		.bcor$ = "^008aff[»]^000000 ";
		.rcor$ = "^ff0000[»]^000000 ";
		setarray .vip_days, 7, 14, 30;
		setarray .vip_prices, 4000, 7000, 10000;
		.vip_map$ = "vip_lounge";
		setarray .vip_coords, 152, 120;
		.cooldown = 30; // segundos

		// Bind do comando @vip
		unbindatcmd "vip";
		bindatcmd "vip", strnpcinfo(3) + "::OnVIP", 0, 100;

		callfunc("F_NpcInitStatus", "Sala VIP");
		setunitdata getnpcid(0), UNPC_GROUP_ID, 52;
		end;

	OnVIP:
		if (@lastvip && gettimetick(2) - @lastvip < .cooldown) {
			dispbottom "Espere " + (.cooldown - (gettimetick(2) - @lastvip)) + " segundos para usar o comando novamente.";
			end;
		}
		@lastvip = gettimetick(2);

		callsub ShowMainMenu;
		end;

	ShowMainMenu:
		mes .npc_name$;

		set .@hora, gettime(3);
		mes ((.@hora >= 6 && .@hora <= 12) ? "Bom dia" : ((.@hora >= 13 && .@hora <= 18) ? "Boa tarde" : "Boa noite")) + ", ^008aff" + strcharinfo(0) + "^000000!";
		mes "Bem-vindo à Central VIP.";

		if (vip_status(1)) {
			mes "^008affSeu acesso VIP expira em:^000000 " + callfunc("Time2Str", vip_status(2)) + ".";
			mes " ";
		}

		mes "O que deseja fazer?";
		next;

		set .@opt, select(
			.bcor$ + "Entrar na Central VIP",
			.bcor$ + ((vip_status(1)) ? "Estender tempo ^008affVIP^000000" : "Tornar-se jogador ^008affVIP^000000"),
			.rcor$ + "Sair"
		);

		switch (.@opt) {
			case 1:
				if (!vip_status(1)) {
					mes .npc_name$;
					mes "Desculpe! O acesso à Central VIP é permitido somente a jogadores ^008affVIP^000000.";
					close;
				}
				warp .vip_map$, .vip_coords[0], .vip_coords[1];
				end;

			case 2:
				callsub PurchaseVIP;
				end;

			case 3:
				mes .npc_name$;
				mes "Certo, até a próxima.";
				close;
		}

	PurchaseVIP:
		mes .npc_name$;
		mes "Quantos dias de acesso ^008affVIP^000000 você deseja adquirir?";
		next;

		set .@i, select(
			.bcor$ + .vip_days[0] + " Dias",
			.bcor$ + .vip_days[1] + " Dias",
			.bcor$ + .vip_days[2] + " Dias"
		) - 1;

		mes .npc_name$;
		mes "Você escolheu:";
		mes "^4527A0VIP:^000000 " + .vip_days[.@i] + " Dias";
		mes "^4527A0Preço:^000000 " + F_InsertComma(.vip_prices[.@i]) + " #CASHPOINTS";
		mes " ";
		mes "Deseja continuar?";
		next;

		if (select(.bcor$ + "Confirmar compra", .rcor$ + "Cancelar") == 1) {
			if (#CASHPOINTS < .vip_prices[.@i]) {
				mes .npc_name$;
				mes "Você não possui ^008aff" + F_InsertComma(.vip_prices[.@i]) + " #CASHPOINTS^000000.";
				close;
			}
			#CASHPOINTS -= .vip_prices[.@i];
			vip_time(.vip_days[.@i] * 1440);
			mes .npc_name$;
			mes "Parabéns! Você adquiriu " + .vip_days[.@i] + " dias de acesso VIP.";
			close;
		} else {
			mes .npc_name$;
			mes "Tudo bem, volte sempre que quiser.";
			close;
		}
}
