// Main NPC :: safety_Ref_NPC
//============================================================
prt_in,94,67,2	script	Mestre do Refino	826,{
	disable_items;
if (countitem(6238) || countitem(6228) || countitem(6229) || countitem(6230) || countitem(6231) || countitem(6456))
	set .@bWeaponUp,1;
if (countitem(6239) || countitem(6232) || countitem(6233) || countitem(6234) || countitem(6235) || countitem(6457))
	set .@bArmorUp,1;
if (!.@bWeaponUp && !.@bArmorUp) {
	mes "[Mestre do Refino]";
	mes "Olá!";
	mes "Como vai?";
	mes "Eu sou um especialista";
	mes "em refinar itens,";
	mes "mas eu não trabalho mais.";
	next;
	switch(select("Vou seguir meu caminho.:Hmm... isso me deixa curioso.")) {
	case 1:
		mes "[Mestre do Refino]";
		mes "Cuide-se, aventureiro.";
		close;
	case 2:
		mes "[Mestre do Refino]";
		mes "Na verdade, às vezes eu forneço serviços de refino para aventureiros com um ^006400Refine Ticket^000000...";
		mes "Tchau~!";
		close;
	}
}
emotion ET_SURPRISE;
mes "[Mestre do Refino]";
mes "Saudações!";
mes "Posso refinar um item até o ^006400mesmo nível do seu ticket^000000.";
mes "Você não precisa se preocupar! Não há chance de quebrar seu item.";
next;
if(select("Voltarei mais tarde.:Refinar item com ticket.") == 1) {
	mes "[Mestre do Refino]";
	mes "Ok.";
	mes "Você pode voltar mais tarde.";
	close;
}
mes "[Mestre do Refino]";
mes "Qual equipamento você gostaria de refinar?";
next;
setarray .@position$[1],"Cabeça Superior","Armadura","Mão Esquerda","Mão Direita","Manto","Sapatos","Acessório 1","Acessório 2","Cabeça Meio","Cabeça Inferior";
setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
for(set .@i,1; .@i<=10; set .@i,.@i+1)
	set .@menu$, .@menu$+((getequipisequiped(.@indices[.@i]))?getequipname(.@indices[.@i]):.@position$[.@i]+"- [Vazio]")+":";
set .@part, .@indices[ select(.@menu$) ];
if (!getequipisequiped(.@part)) {
	mes "[Mestre do Refino]";
	mes "Você precisa equipar o item que deseja refinar.";
	close;
}
if (!getequipisenableref(.@part)) {
	emotion ET_OTL;
	mes "[Mestre do Refino]";
	mes "Oh, sinto muito.";
	mes "Este item não pode ser refinado.";
	close;
}
switch(getequipweaponlv(.@part)) {
default:
case 0:
	setarray .@tickets[0],6457,6235,6234,6233,6232,6239;
	setarray .@levels[0],5,6,7,8,9,11;
	set .@type$,"Armadura";
	set .@check,.@bArmorUp;
	break;
case 1:
case 2:
case 3:
case 4:
	setarray .@tickets[0],6456,6231,6230,6229,6228,6238;
	setarray .@levels[0],5,6,7,8,9,11;
	set .@type$,"Arma";
	set .@check,.@bWeaponUp;
	break;
}
if (!.@check) {
	emotion ET_THINK;
	mes "[Mestre do Refino]";
	mes "Se você quiser refinar esta ^006400"+.@type$+"^000000, por favor, traga junto um ^006400"+.@type$+" Refine Ticket^000000.";
	mes "Até mais tarde!";
	close;
}
mes "[Mestre do Refino]";
mes "Por favor, escolha qual ^006400"+.@type$+" Refine Ticket^000000 você deseja usar.";
next;
set .@menu$,"";
for(set .@i,0; .@i<getarraysize(.@tickets); set .@i,.@i+1)
	set .@menu$, .@menu$+getitemname(.@tickets[.@i])+":";
set .@select, select(.@menu$)-1;
set .@ticket_lv, .@levels[.@select];
set .@ticket_id, .@tickets[.@select];
if (countitem(.@ticket_id) == 0) {
	emotion ET_QUESTION;
	mes "[Mestre do Refino]";
	mes getitemname(.@ticket_id)+" não está no seu inventário. Você colocou no seu armazém?";
	mes "Por favor, verifique novamente.";
	mes "Até mais tarde!";
	close;
}
if (getequiprefinerycnt(.@part) >= .@ticket_lv) {
	emotion ET_PROFUSELY_SWEAT;
	mes "[Mestre do Refino]";
	mes "^8B4513Este item já foi refinado tanto quanto seu ticket.^000000";
	mes "Por favor, traga um item refinado menos que o seu ticket.";
	close;
}
mes "[Mestre do Refino]";
mes "Vou refinar ^006400"+getequipname(.@part)+"^8B4513 até o +"+.@ticket_lv+" nível^000000 com ^006400"+getitemname(.@ticket_id)+"^000000.";
mes "Posso prosseguir?";
next;
if(select("Não.:Sim.") == 1) {
	emotion ET_THINK;
	mes "[Mestre do Refino]";
	mes "Ah, você mudou de ideia.";
	mes "Ok.";
	mes "Você pode voltar mais tarde.";
	close;
}
mes "[Mestre do Refino]";
mes "Ótimo.";
mes "Como você desejar!";
mes "Eu tenho meu próprio jeito especial de refinar...";
mes ".......ka boom!";
specialeffect EF_SUI_EXPLOSION;
if (countitem(.@ticket_id))
	delitem .@ticket_id,1;
else {
	next;
	mes "Erro!";
	mes "Por favor, relate isso.";
	close;
}
successrefitem .@part, .@ticket_lv - getequiprefinerycnt(.@part);
next;
emotion ET_DELIGHT;
mes "[Mestre do Refino]";
mes "Certo, aqui está~";
mes "Bom, ^0000FF"+strcharinfo(0)+"^000000!";
mes "Parabéns "+.@type$+" conquistado.";
mes "Você está melhorando !";
mes "Ate a Proxima~!";
close;

}
	
	