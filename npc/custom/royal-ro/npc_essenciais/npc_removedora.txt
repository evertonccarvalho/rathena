//===== rAthena Script =======================================
//= Card Removal NPC
//===== By: ==================================================
//= TyrNemesis^
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Removes cards from equipped items.
//===== Additional Comments: =================================
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2a Added 'disable_items' command. [Euphy]
//============================================================
prontera,130,216,5	script	Remover Cartas#eAcustom	961,{
	set .zenycost,1000000;    // Preço padrão para uso do serviço (em Zeny)
	set .percardcost,25000;  // Preço por carta para uso do serviço (in Zeny)
	set .faildestroy,1;      // Há a chance do equipamento ser quebrado? (1=sim, 0=não)
	disable_items;
	mes "^339966[ Velha Removedora ]^000000";
	mes (gettime(3)>= 6&&gettime(3)<= 12?"Bom-dia":(gettime(3)>=13&&gettime(3)<=18?"Boa-tarde":"Boa-noite"))+", ^008aff"+strcharinfo(0)+"^000000 !";
	mes "Eu tenho o poder de remover cartas de quaisquer equipamentos. Essa ideia lhe agrada?";
	next;
	switch(select("^3CB371[»]^000000 Sim.:^3CB371[»]^000000 Quanto você cobra?:^ff0000[»]^000000 Não, obrigado.")) {
	case 1:
		mes "^339966[ Velha Removedora ]^000000";
		mes "Muito bem, qual item você quer que eu examine?";
		next;
		setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 ) {
			if( getequipisequiped(.@indices[.@i]) )
				set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@menu$, .@menu$ + ":";
		}
		set .@part, .@indices[ select(.@menu$) ];
		if(!getequipisequiped(.@part)) {
			mes "^339966[ Velha Removedora ]^000000";
			mes "Calma ai jovem... Não há nenhum equipamento na área selecionada.";
			close;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "^339966[ Velha Removedora ]^000000";
			mes "Calma ai jovem.. Não há nenhuma carta anexada a este equipamento. Não posso fazer nada com ele.";
			close;
		}
		set .@cardcount,getequipcardcnt(.@part);
		
		if (!checkweight(1202,(.@cardcount+1))) { 
			mes "^3355FFSó um minuto!";
			mes "Eu não posso oferecer qualquer um dos meus";
			mes "para você, porque";
			mes "você está carregando muito";
			mes "material. Coloque seus itens extras em";
			mes "seu storage e volte a falar comigo.";
			close;
		}
		mes "^339966[ Velha Removedora ]^000000";
		mes "Este equipamento possui " + .@cardcount + " cartas anexadas a ele. Para executar minha magia vou precisar de " + (.zenycost+(.@cardcount * .percardcost)) + " zeny, um ^0000FFFragmento Estelar^000000, e uma ^0000FFGema Amarela^000000.";
		next;
		if(select("^3CB371[»]^000000 Muito bem. Faça.:^ff0000[»]^000000 Deixa pra lá.") == 2) {
			mes "^339966[ Velha Removedora ]^000000";
			mes "Tudo bem. Retorne se você precisar de meus serviços.";
			close;
		}
		if((zeny < (.zenycost+(.@cardcount * .percardcost))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
			mes "^339966[ Velha Removedora ]^000000";
			mes "Você não possui todos os itens necessários para executar minha magia. Volte quando possuir todos.";
			close;
		}
		mes "^339966[ Velha Removedora ]^000000";
		mes "Antes de começar devo avisá-lo que minha magia pode falhar. Se acontecer isso, corro o risco de destruir as cartas, o item ou ambos.";
		next;
		mes "^339966[ Velha Removedora ]^000000";
		mes "Dito isso o que é mais importante para você?";
		next;
		switch(select("^ff0000[»]^000000 Eu mudei de ideia sobre isso.:^3CB371[»]^000000 O item.:^3CB371[»]^000000 As cartas.")) {
		case 1:
			mes "^339966[ Velha Removedora ]^000000";
			mes "Tudo bem. Retorne se você precisar de meus serviços.";
			close;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "^339966[ Velha Removedora ]^000000";
		mes "Muito bem. Vou começar.";
		set Zeny, Zeny - (.zenycost+(.@cardcount * .percardcost));
		delitem 1000,1; //Star_Crumb
		delitem 715,1; //Yellow_Gemstone
		
		// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
		// First value = Total failure chance (item and cards destroyed)
		// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
		// Third value = Harmless failure chance (all that's lost is your investment)
		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "^339966[ Velha Removedora ]^000000";
				mes "O processo foi um fracasso total. Receio que o item e as cartas foram destruídas.";
				close;
			}
			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "^339966[ Velha Removedora ]^000000";
					mes "Embora eu tenha conseguido remover as cartas do item, elas foram destruídas durante o processo. O item porém está intacto.";
					close;
				}
				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "^339966[ Velha Removedora ]^000000";
					mes "Consegui remover as cartas mas infelismente não consegui salvar o item. O mesmo foi destruido durante o processo.";
					close;
				}
			}
		}
		if(.@failchance < 10) {
			next;
			failedremovecards .@part,3;
			mes "^339966[ Velha Removedora ]^000000";
			mes "Eu não consegui remover as cartas. Porém o item e as cartas estão intactas.";
			close;
		}
		next;
		successremovecards .@part;
		mes "^339966[ Velha Removedora ]^000000";
		mes "O processo foi um sucesso! Aqui está seu item e suas cartas.";
		close;
	case 2:
		mes "^339966[ Velha Removedora ]^000000";
		mes "Eu cobro uma taxa fixa de "+callfunc("F_InsertComma",.zenycost)+" zeny, além de "+callfunc("F_InsertComma",.percardcost)+" zeny por cada carta que será removida. Em adicional, eu preciso de uma Fragmento Estelar e uma Gema Amarela para realizar minha magia.";
		close;
	case 3:
		mes "^339966[ Velha Removedora ]^000000";
		mes "Tudo bem. Retorne se você precisar de meus serviços.";
		close;
	}

OnInit:
	.NPCName$ = callfunc("F_NpcInitStatus", "Removedora", 41);
end;

}